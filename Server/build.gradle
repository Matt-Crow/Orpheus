plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':Core')

    // https://mvnrepository.com/artifact/javax.json/javax.json-api
    implementation 'javax.json:javax.json-api:1.1.4'
    implementation 'org.glassfish:javax.json:1.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

application {
    mainClass = 'orpheus.server.Main'
}

tasks.register('uberjar', Jar) { // extends Jar
    dependsOn tasks.jar

    archiveBaseName = 'OrpheusServer'
    duplicatesStrategy = DuplicatesStrategy.WARN

    manifest {
        attributes['Main-Class'] = application.mainClass
    }

    from {
        configurations.runtimeClasspath.findAll{ it.name.endsWith('.jar') }.collect{zipTree(it)}
    }
    with jar
}

tasks.named('test') {
    useJUnitPlatform()
}